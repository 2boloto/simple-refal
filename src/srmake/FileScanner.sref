//FROM LibraryEx
$EXTERN LoadFile, Map, Trim, Fetch;

//FROM FindFile
$EXTERN FindFiles;

//FROM Library
$EXTERN ExistFile;

$ENTRY CreateFileList {
  (e.Folders) e.Unit =
    <DoCreateFileList
      (e.Folders) () <FindFiles (e.Folders) (e.Unit)>
    >;
}

DoCreateFileList {
  (e.Folders) (e.Modules) = e.Modules;

  (e.Folders) (e.Modules-B (e.NextModule) e.Modules-E)
  (e.NextModule) e.NotScanned =
    <DoCreateFileList
      (e.Folders) (e.Modules-B (e.NextModule) e.Modules-E)
      e.NotScanned
    >;

  (e.Folders) (e.Modules) (e.NextModule) e.NotScanned =
    <DoCreateFileList
      (e.Folders) (e.Modules (e.NextModule))
      <LoadList (e.Folders) e.NextModule> e.NotScanned
    >;
}

FindImports {
  e.Folders ('//FROM ' e.Name) =
    <FindFiles (e.Folders) (<Trim e.Name>)>;

  e.Folders (e.OtherLine) = ;
}

LoadList {
  (e.Folders) #Source (e.NextModule) e.Output =
    <Map
      (FindImports e.Folders) <LoadFile e.NextModule>
    >;

  (e.Folders) #Output e.Output =
    <Map
      (FindImports e.Folders)
      <LoadFile e.Output>
      <LoadIfExist e.Output '.froms'>
    >;

  (e.Folders) #OutputWithNative (e.Output) e.Native =
    <Map
      (FindImports e.Folders)
      <LoadFile e.Output>
      <LoadIfExist e.Output '.froms'>
      <LoadFile e.Native>
    >;

  (e.Folders) #NotFound e.Unit = ;
}

LoadIfExist {
  e.FileName =
    <Fetch
      <ExistFile e.FileName>
      {
        #True = <LoadFile e.FileName>;
        #False = /* пусто */;
      }
    >;
}
