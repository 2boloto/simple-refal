//FROM LibraryEx
$EXTERN Map, Fetch;

//FROM Library
$EXTERN ExistFile;

/**
  <FindFiles (e.Folders) e.Files>
    == t.FoundFile*

  t.FoundFile ::=
    (#Source (e.Source) e.Output)
    (#Output e.Output)
    (#OutputWithNative (e.Output) e.Native)
    (#NotFound e.FileName)
*/
$ENTRY FindFiles {
  (e.Folders) e.Files =
    <Map (AnalyzeFile-ByFolders #Current e.Folders) e.Files>;
}

AnalyzeFile-ByFolders {
  e.Folders (e.FileName) =
    <AnalyzeFile-CheckNotFound
      (e.FileName)
      <Map (AnalyzeInFolder e.FileName) e.Folders>
    >;
}

AnalyzeInFolder {
  e.FileName #Current = <AnalyzeFile e.FileName>;

  e.FileName (e.Folder) = <AnalyzeFile e.Folder '/' e.FileName>;
}

AnalyzeFile-CheckNotFound {
  (e.FileName) (#Source (e.Source) e.Output) e.Variants =
    (#Source (e.Source) e.Output);

  (e.FileName) (#Output e.Output) e.Variants =
    (#Output e.Output);

  (e.FileName) (#OutputWithNative (e.Output) e.Native) e.Variants =
    (#OutputWithNative (e.Output) e.Native);

  (e.FileName) (#NotFound e.NotFoundPath) e.Variants =
    <AnalyzeFile-CheckNotFound (e.FileName) e.Variants>;

  (e.FileName) = (#NotFound e.FileName);
}

ExistFile-T {
  e.FileName = <ExistFile e.FileName> e.FileName;
}

AnalyzeFile {
  e.FileName '.sref' =
    <Fetch
      <ExistFile-T e.FileName '.sref'>
      {
        #True e.UnitName '.sref' =
          (#Source (e.UnitName '.sref') e.UnitName '.cpp');

        #False e.SourceName = (#NotFound e.SourceName);
      }
    >;

  e.FileName '.cpp' =
    <Fetch
      <ExistFile-T e.FileName '.cpp'>
      {
        #True e.OutName =
          <Fetch
            <ExistFile-T e.FileName '.native.cpp'>
            {
              #True e.NativeName =
                (#OutputWithNative (e.OutName) e.NativeName);

              #False e.NativeName =
                (#Output e.OutName);
            }
          >;

        #False e.OutName = (#NotFound e.OutName);
      }
    >;

  e.FileName =
    <Fetch
      (<ExistFile-T e.FileName '.sref'>) <ExistFile-T e.FileName '.cpp'>
      {
        (#True e.SourceName) s.Res e.OutName =
          (#Source (e.SourceName) e.OutName);

        (#False e.SourceName) #True e.OutName =
          (#Output e.OutName);

        (#False e.UnitName '.sref') #False e.UnitName '.cpp' =
          (#NotFound e.UnitName);
      }
    >;
}
