//FROM LibraryEx
$EXTERN Map;

//FROM HighLevelRASL-DisjointFunc
$EXTERN HighLevelRASL-Function-Disjoint;

//FROM HighLevelRASL-ConjointFunc
$EXTERN HighLevelRASL-Function-Conjoint;

//FROM HighLevelRASL-GenSubst-Simple
$EXTERN GenInitSubst-Simple, GenSubst-Simple;

//FROM HighLevelRASL-GenResult-Simple
$EXTERN GenResult-Simple;

//FROM HighLevelRASL-GenSubst-Save
$EXTERN GenInitSubst-Save, GenSubst-Save;

//FROM HighLevelRASL-GenResult-Opt
$EXTERN GenResult-Opt;

/**
  <HighLevelRASL e.AST>
    == e.RASLAST

  e.RASLAST ::= t.RASLFunction*
  t.RASLFunction ::=
      (#Function s.ScopeClass (e.Name) t.HiRASLCommand*)
    | (s.SingularItem s.ScopeClass e.Name)
    | (#Ident e.Name)
    | (#Separator)
  s.SingularItem ::= #Enum | #Swap | #Stub | #Declaration
  t.HiRASLCommand ::=
      (#Cmd… e.Value)
    | (#CmdSentence t.HiRASLCommand*)
    | (#CmdOpenELoop #AlgLeft s.BracketNum s.VarNumber t.HiRASLCommand*)
*/
$ENTRY HighLevelRASL {
  s.Joint s.OptResult e.ProgramElements =
    <Map
      {
        (#Function s.ScopeClass (e.Name) /* нет предложений */) =
          (#CmdEnum s.ScopeClass e.Name);

        (#Function s.ScopeClass (e.Name) e.Sentences) =
          <HighLevelRASL-Function
            <GenSentenceFunc s.Joint>
            <GenResultFuncs s.OptResult>
            s.ScopeClass (e.Name) e.Sentences
          >;

        (#Enum s.ScopeClass e.Name) =
          (#CmdEnum s.ScopeClass e.Name);

        (#Swap s.ScopeClass e.Name) =
          (#CmdSwap s.ScopeClass e.Name);

        (#Stub s.ScopeClass e.Name) =
          /* пусто */;

        (#Declaration s.ScopeClass e.Name) =
          (#CmdDeclaration s.ScopeClass e.Name);

        (#Ident e.Name) = (#CmdDefineIdent e.Name);

        (#Separator) = (#CmdSeparator);
      }
      e.ProgramElements
    >;
}

HighLevelRASL-Function {
  s.FnGenFunction s.FnGenInitSubst s.FnGenSubst s.FnGenResult
  s.ScopeClass (e.Name) e.Sentences =
    <s.FnGenFunction
      s.FnGenInitSubst s.FnGenSubst s.FnGenResult
      s.ScopeClass (e.Name) e.Sentences
    >;
}

GenSentenceFunc {
  #Conjoint = HighLevelRASL-Function-Conjoint;
  #Disjoint = HighLevelRASL-Function-Disjoint;
}

GenResultFuncs {
  #NoOpt     = GenInitSubst-Simple GenSubst-Simple GenResult-Simple;
  #OptResult = GenInitSubst-Save   GenSubst-Save   GenResult-Opt;
}
