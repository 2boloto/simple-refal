*$FROM R5-Utils
$EXTERN R5-Builtins;

$ENTRY R5-AddMu {
  e.AST
    , <R5-Builtins> : e.Builtins
    /* Пока они косвенно не вызываются */
    , <DoAddMu-Names e.Builtins> : e.MuBuiltins
    , <DoAddMu-AST () e.AST> : (e.MuAST) e.AST
    = (Function NoPos ('Mu') GN-Local e.MuBuiltins e.MuAST) e.AST;
}

DoAddMu-Names {
  /* Служебное имя, пропускаем */
  (e.NextName '$$') e.Tail = <DoAddMu-Names e.Tail>;

  /* Пустое имя, пропускаем */
  () e.Tail = <DoAddMu-Names e.Tail>;

  (e.NextName) e.Tail = <MuSentence e.NextName> <DoAddMu-Names e.Tail>;
  /* пусто */ = /* пусто */;
}

MuSentence {
  e.Name
    = <MuSentence-Special e.Name>
      (((TkIdentifier e.Name) <Var-eX>) RETURN (<Call-eX e.Name>))
      (((Brackets <Chars e.Name>) <Var-eX>) RETURN (<Call-eX e.Name>));
}

Var-eX {
  = (Variable NoPos 'eX');
}

Call-eX {
  s.SugarName
    , ('+Add') ('-Sub') ('*Mul') ('/Div') ('%Mod') ('?Residue')
    : e.SugarNames-B (s.SugarName e.RealName) e.SugarNames-E
    = <Call-eX e.RealName>;

  e.Name = (Call NoPos (e.Name) <Var-eX>);
}

MuSentence-Special {
  s.Name, '+-*/%?' : e.Chars-B s.Name e.Chars-E
    = (((TkChar s.Name) <Var-eX>) RETURN (<Call-eX s.Name>));

  e.OtherName = /* пусто */;
}

Chars {
  s.Char e.Chars = (TkChar s.Char) <Chars e.Chars>;
  /* пусто */ = /* пусто */;
}

DoAddMu-AST {
  (e.MuSentences) e.Tail (Extern e.Names)
    = <DoAddMu-AST (<DoAddMu-Names e.Names> e.MuSentences) e.Tail>
      (Extern e.Names);

  (e.MuSentences) e.Tail (Function t.SrcPos (e.Name) s.Scope e.Sentences)
    = <DoAddMu-AST (<MuSentence e.Name> e.MuSentences) e.Tail>
      (Function t.SrcPos (e.Name) s.Scope e.Sentences);

  (e.MuSentences) /* пусто */ = (e.MuSentences);
}
