*$FROM R5-Utils
$EXTERN R5-ArgList, R5-PrintLines, R5-Builtins, R5-SaveFile;

*$FROM R5-Tests
$EXTERN R5-RunTests;

*$FROM R5-Main
$EXTERN R5-Preprocess;

$ENTRY Go {
  = <Main <R5-ArgList>>;
}

Title { = 'Convertor full Refal-5 sources to Basis Refal subset'; }

Main {
  (e.ProgName) (e.Source) (e.Destination) e.Skipped
    = <R5-PrintLines
        (<Title>)
        ('Source:      ' e.Source)
        ('Destination: ' e.Destination)
      >
      <Main-Preprocess (e.Source) (e.Destination)>;

  (e.ProgName) ('_tests_')
    = <R5-RunTests>;

  (e.ProgName) e.Other =
    <R5-PrintLines
      (<Title>)
      ('Bad command line. Expected command line syntax:')
      ('  refgo ' e.ProgName ' source.ref destination.ref')
    >;
}

Main-Preprocess {
  (e.Source) (e.Destination)
    , <R5-Preprocess e.Source>
    : {
        Success e.Lines =
          <R5-SaveFile (e.Destination) e.Lines>;

        Fails e.Errors =
          <R5-PrintLines
            <FormatErrors e.Errors>
          >;
      };
}

FormatErrors {
  ((s.Line s.Col e.FileName) e.Message) e.Errors =
    <Prout e.FileName ':' <Symb s.Line> ':' <Symb s.Col> ':' e.Message>
    <FormatErrors e.Errors>;

  /* пусто */ = /* всё */
}
