*$FROM R5-Utils
$EXTERN R5-ArgList, R5-PrintLines, R5-SaveFile;

*$FROM R5-Lexer
$EXTERN R5-Scan;

*$FROM R5-Parser
$EXTERN R5-Parse;

*$FROM R5-Transformer
$EXTERN R5-Transform;

*$FROM R5-Plainer
$EXTERN R5-Plain;

*$FROM R5-Tests
$EXTERN R5-RunTests;

$ENTRY Go {
  = <Main <R5-ArgList>>;
}

Title { = 'Convertor full Refal-5 sources to Basis Refal subset'; }

Main {
  (e.ProgName) (e.Source) (e.Destination) e.Skipped,
      <R5-PrintLines
        (<Title>)
        ('Source:      ' e.Source)
        ('Destination: ' e.Destination)
      >
      <R5-Parse <R5-Scan e.Source>>:
    {
      Success e.Functions =
        <R5-SaveFile
          (e.Destination)
          <R5-Plain <R5-Transform e.Functions>>
        >;

      Fails e.Errors =
        <R5-PrintLines
          <FormatErrors e.Errors>
        >;
    };

  (e.ProgName) ('_tests_')
    = <R5-RunTests>;

  (e.ProgName) e.Other =
    <R5-PrintLines
      (<Title>)
      ('Bad command line. Expected command line syntax:')
      ('  refgo ' e.ProgName ' source.ref destination.ref')
    >;
}

FormatErrors {
  ((s.Line s.Col e.FileName) e.Message) e.Errors =
    <Prout e.FileName ':' <Symb s.Line> ':' <Symb s.Col> ':' e.Message>
    <FormatErrors e.Errors>;

  /* пусто */ = /* всё */
}
