/**
  <R5-ConvertAST t.Units*> == e.AST
*/
$ENTRY R5-ConvertAST {
  e.Units = <Map ConvertAST-Unit e.Units>;
}

Map {
  s.Func t.Item e.Items = <Mu s.Func t.Item> <Map s.Func e.Items>;

  s.Func /* пусто */ = /* пусто */
}

ConvertAST-Unit {
  (Extern e.Names) = <Map ConvertExtern e.Names>;

  (Function t.SrcPos (e.Name) s.Scope e.Sentences)
    = <ConvertFunction t.SrcPos (e.Name) s.Scope e.Sentences>;
}

ConvertExtern {
  (e.ExternName) = (Declaration NoPos GN-Entry e.ExternName);
}

ConvertFunction {
  t.SrcPos (e.Name) s.Scope e.Sentences
    = (Function
        t.SrcPos s.Scope (e.Name)
        Sentences <Map ConvertSentence e.Sentences>
      );
}

ConvertSentence {
  /*
    Здесь полагаем, что работаем после трансформера: нет ни условий, ни блоков.
  */
  ((e.Pattern) RETURN (e.Result))
    = ((e.Pattern) (e.Result) (/* нет блоков */));
}
