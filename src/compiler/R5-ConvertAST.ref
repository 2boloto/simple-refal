/**
  <R5-ConvertAST t.Units*> == e.AST
*/
$ENTRY R5-ConvertAST {
  e.Units = <Map ConvertAST-Unit e.Units>;
}

Map {
  s.Func t.Item e.Items = <Mu s.Func t.Item> <Map s.Func e.Items>;

  s.Func /* пусто */ = /* пусто */
}

ConvertAST-Unit {
  (Declaration t.SrcPos GN-Entry e.ExternName)
    = (Declaration t.SrcPos GN-Entry e.ExternName);

  (Function t.SrcPos s.Scope (e.Name) Sentences e.Sentences)
    = (Function
        t.SrcPos s.Scope (e.Name)
        Sentences <Map ConvertSentence e.Sentences>
      );
}

ConvertSentence {
  /*
    Здесь полагаем, что работаем после трансформера: нет ни условий, ни блоков.
  */
  ((e.Pattern) RETURN (e.Result))
    = ((e.Pattern) (e.Result) (/* нет блоков */));
}
