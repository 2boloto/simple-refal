//FROM LibraryEx
$EXTERN Fetch, Seq, SaveFile;

//FROM Lexer
$EXTERN LexFolding;

//FROM Parser
$EXTERN ParseProgram;

//FROM Error
$EXTERN EL-Create, EL-Destroy;

//FROM Desugaring
$EXTERN Desugar;

//FROM HighLevelRASL
$EXTERN HighLevelRASL;

//FROM LowLevelRASL
$EXTERN LowLevelRASL;

//FROM Generator
$EXTERN GenProgram;

/**
  <CompileFile s.GenMode s.Opt (e.SrcName) e.OutputName>
    == #Success e.OutputName
    == #Fails

  s.GenMode ::= #OnlyDirect | #OnlyInterpret | #Both
  s.Opt ::= #OptNone | #OptPattern | #OptResult | #OptBoth
*/
$ENTRY CompileFile {
  s.GenMode s.Opt (e.SrcName) e.OutputName =
    <Fetch
      e.SrcName
      <Seq
        LexFolding
        (ParseProgram <EL-Create e.SrcName>)
        {
          t.ErrorList e.AST =
            <Fetch
              <EL-Destroy t.ErrorList>
              {
                #EL-NoErrors =
                  <Fetch
                    e.AST
                    <Seq
                      Desugar
                      (HighLevelRASL <SelectOptFlags s.Opt>)
                      (LowLevelRASL s.GenMode)
                      GenProgram
                      (SaveFile (e.OutputName))
                    >
                  >
                  #Success e.OutputName;

                #EL-HasErrors =
                  #Fails;
              }
            >;
        }
      >
    >;
}

SelectOptFlags {
  #OptNone    = #Disjoint #NoOpt;
  #OptPattern = #Conjoint #NoOpt;
  #OptResult  = #Disjoint #OptResult;
  #OptBoth    = #Conjoint #OptResult;
}
