//FROM LibraryEx
$EXTERN Fetch, Seq, SaveFile;

//FROM LoadSource
$EXTERN LoadSource;

//FROM Lexer
$EXTERN LexFolding;

//FROM Parser
$EXTERN ParseProgram;

//FROM Error
$EXTERN EL-Create, EL-Destroy;

//FROM Checker
$EXTERN CheckProgram;

//FROM Desugaring
$EXTERN Desugar;

//FROM HighLevelRASL
$EXTERN HighLevelRASL;

//FROM LowLevelRASL
$EXTERN LowLevelRASL;

//FROM Generator
$EXTERN GenProgram-RASL, GenProgram-Native;

/**
  <CompileFile s.GenMode s.Opt s.MarkupContext (e.SrcName) e.OutputName>
    == #Success e.OutputName t.Native
    == #Fails

  s.GenMode ::= #OnlyDirect | #OnlyInterpret | #Both
  s.Opt ::= #OptNone | #OptPattern | #OptResult | #OptBoth
  s.MarkupContext ::= #MarkupContext | #NoMarkupContext
  t.Native ::= #NoNative | (e.NativeOutputName)
*/
$ENTRY CompileFile {
  s.GenMode s.Opt s.MarkupContext (e.SrcName) e.OutputName '.cpp' =
    <Fetch
      e.SrcName
      <Seq
        LoadSource
        LexFolding
        (ParseProgram <EL-Create e.SrcName>)
        {
          t.ErrorList e.AST =
            <CheckProgram t.ErrorList e.AST>
            e.AST;
        }
        {
          t.ErrorList e.AST =
            <Fetch
              <EL-Destroy t.ErrorList>
              {
                #EL-NoErrors =
                  <Fetch
                    e.AST
                    <Seq
                      (Desugar s.MarkupContext)
                      (HighLevelRASL <SelectOptFlags s.Opt>)
                      (LowLevelRASL s.GenMode (e.SrcName))
                      {
                        e.RASL (e.NativeRASL) =
                          <Fetch
                            e.RASL
                            <Seq
                              GenProgram-RASL
                              (SaveFile (e.OutputName '.cpp'))
                            >
                          >
                          #Success e.OutputName '.cpp'
                          <Fetch
                            (e.NativeRASL)
                            {
                              () = #NoNative;

                              (e.NativeRASL^) =
                                <Fetch
                                  e.NativeRASL
                                  <Seq
                                    (GenProgram-Native
                                      (e.SrcName) (e.OutputName '.native.cpp')
                                    )
                                    (SaveFile (e.OutputName '.native.cpp'))
                                  >
                                >
                                (e.OutputName '.native.cpp');
                            }
                          >;
                      }
                    >
                  >;

                #EL-HasErrors =
                  #Fails;
              }
            >;
        }
      >
    >;
}

SelectOptFlags {
  #OptNone    = #Disjoint #NoOpt;
  #OptPattern = #Conjoint #NoOpt;
  #OptResult  = #Disjoint #OptResult;
  #OptBoth    = #Conjoint #OptResult;
}
