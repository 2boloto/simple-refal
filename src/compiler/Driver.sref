//FROM LibraryEx
$EXTERN Fetch, Seq, SaveFile;

//FROM LoadSource
$EXTERN LoadSource;

//FROM Lexer
$EXTERN LexFolding;

//FROM Parser
$EXTERN ParseProgram;

//FROM Error
$EXTERN EL-Create, EL-Destroy;

//FROM Checker
$EXTERN CheckProgram;

//FROM Desugaring
$EXTERN Desugar;

//FROM HighLevelRASL
$EXTERN HighLevelRASL;

//FROM LowLevelRASL
$EXTERN LowLevelRASL;

//FROM Generator
$EXTERN GenProgram;

/**
  <CompileFile s.GenMode s.Opt s.MarkupContext (e.SrcName) e.OutputName>
    == #Success e.OutputName
    == #Fails

  s.GenMode ::= #OnlyDirect | #OnlyInterpret | #Both
  s.Opt ::= #OptNone | #OptPattern | #OptResult | #OptBoth
  s.MarkupContext ::= #MarkupContext | #NoMarkupContext
*/
$ENTRY CompileFile {
  s.GenMode s.Opt s.MarkupContext (e.SrcName) e.OutputName =
    <Fetch
      e.SrcName
      <Seq
        LoadSource
        LexFolding
        (ParseProgram <EL-Create e.SrcName>)
        {
          t.ErrorList e.AST =
            <CheckProgram t.ErrorList e.AST>
            e.AST;
        }
        {
          t.ErrorList e.AST =
            <Fetch
              <EL-Destroy t.ErrorList>
              {
                #EL-NoErrors =
                  <Fetch
                    e.AST
                    <Seq
                      (Desugar s.MarkupContext)
                      (HighLevelRASL <SelectOptFlags s.Opt>)
                      (LowLevelRASL s.GenMode)
                      (GenProgram (e.SrcName) (e.OutputName))
                      (SaveFile (e.OutputName))
                    >
                  >
                  #Success e.OutputName;

                #EL-HasErrors =
                  #Fails;
              }
            >;
        }
      >
    >;
}

SelectOptFlags {
  #OptNone    = #Disjoint #NoOpt;
  #OptPattern = #Conjoint #NoOpt;
  #OptResult  = #Disjoint #OptResult;
  #OptBoth    = #Conjoint #OptResult;
}

/**
  <GrammarCheck e.SrcName>
    == #Success
    == #Fails
*/
$ENTRY GrammarCheck {
  e.SrcName =
    <Fetch
      e.SrcName
      <Seq
        LoadSource
        LexFolding
        (ParseProgram <EL-Create e.SrcName>)
        {
          t.ErrorList e.AST =
            <Fetch
              <EL-Destroy t.ErrorList>
              {
                #EL-NoErrors = #Success;
                #EL-HasErrors = #Fails;
              }
            >;
        }
      >
    >;
}
