//FROM LibraryEx
$EXTERN Fetch, Map, Inc, Seq, Dec;

//FROM Error
$EXTERN EL-AddUnexpected, EL-AddErrorAt;

//FROM Context
$EXTERN Cntx-Create, Cntx-Destroy, Cntx-PushScope, Cntx-PopScope,
  Cntx-ResetAfterSentence, Cntx-AddNewVariable, Cntx-AddVariable,
  Cntx-CheckVariable;


$ENTRY ParseProgram {
  t.ErrorList e.Tokens =
    <ParseElements
      t.ErrorList
      (/* AST */)
      <Map PrepareBracket e.Tokens>
    >;
}

PrepareBracket {
  (#TkOpenBracket s.LnNum) = (# Open s.LnNum #TkOpenBracket);
  (#TkCloseBracket s.LnNum) = (# Close s.LnNum #TkCloseBracket);

  (#TkOpenADT s.LnNum) = (# Open s.LnNum #TkOpenADT);
  (#TkCloseADT s.LnNum) = (# Close s.LnNum #TkCloseADT);

  (#TkOpenCall s.LnNum) = (# Open s.LnNum #TkOpenCall);
  (#TkCloseCall s.LnNum) = (# Close s.LnNum #TkCloseCall);

  t.OtherToken = t.OtherToken;
}

/*
  Трансляция осуществляется обычным методом рекурсивного спуска.

  TranslationUnit = Element* .

  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$FORWARD' NameList |
    '$SWAP' NameList |
    '$ESWAP' NameList |
    '$LABEL' NameList |
    '$ENTRY' Function |
    Function .

  NameList = Name ',' NameList | Name ';' .

  Function = Name Block .

  Block = '{' Sentence* '}' .

  Sentence = Pattern '=' Result ';' .

  Pattern = PatternTerm* .

  PatternTerm = CommonTerm |
    '(' Patter ')' | '[' Pattern ']' | RedefinitionVariable .

  RedefinitionVariable = Variable '^' .

  CommonTerm = Char | Number | Name | Variable | '#' Ident .

  Result = ResultTerm* .

  ResultTerm = CommonTerm |
    '(' Result ')' | '[' Result ']' | '<' Result '>' | Block .

  Стратегия обработки ошибок. Выделяем только одну функцию, проглатывающую
  неожиданные лексемы. Остальные функции при обнаружении неожиданной лексемы
  должны её оставить в лексической свёртке перейдя в одно из состояний уровнем
  выше. Данная стратегия обработки ошибок может нарушаться, но при этом должно
  выполняться условие, что #TkEOF передаётся в ParseElements.
*/

/*
  TranslationUnit = Element* .
*/

/*
  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$FORWARD' NameList |
    '$SWAP' NameList |
    '$ESWAP' NameList |
    '$LABEL' NameList |
    '$ENTRY' Function |
    Function .
*/

ParseElements {
  t.ErrorList (e.AST) (#TkEOF s.LnNum) =
    t.ErrorList e.AST;

  t.ErrorList (e.AST) (#TkDirective s.LnNum s.Directive) e.Tail =
    <Fetch
      <ParseElement-SwDirective
        t.ErrorList s.Directive e.Tail
      >
      {
        (e.NewASTItems) t.ErrorList^ e.Tail^ =
          <ParseElements t.ErrorList (e.AST e.NewASTItems) e.Tail>;
      }
    >;

  t.ErrorList (e.AST) (#TkName s.LnNum e.Name) e.Tail =
    <Fetch
      <ParseFunction #GN-Local (e.Name) t.ErrorList e.Tail>
      {
        (e.NewASTItems) t.ErrorList^ e.Tail^ =
          <ParseElements
            t.ErrorList (e.AST e.NewASTItems) e.Tail
          >;
      }
    >;

  t.ErrorList (e.AST) (#TkSemicolon s.LnNum) e.Tail =
    <ParseElements t.ErrorList (e.AST) e.Tail>;

  t.ErrorList (e.AST) t.Unexpected e.Tail =
    <ParseElements
      <EL-AddUnexpected t.ErrorList t.Unexpected 'directive or name'>
      (e.AST) e.Tail
    >;
}

ASTItemFromDirective {
  #EEnum   = #Enum        #GN-Entry;
  #Enum    = #Enum        #GN-Local;
  #ESwap   = #Swap        #GN-Entry;
  #Swap    = #Swap        #GN-Local;

  #Extern  = #Declaration #GN-Entry;

  // Директивы $FORWARD и $LABEL эквивалентны пустому месту
  #Forward = #Declaration #GN-Local;
  // Для меток не важно: entry или local.
  #Ident   = #Ident       #GN-Entry;
}

ParseElement-SwDirective {
  t.ErrorList #Entry (#TkName s.LnNum e.Name) e.Tail =
    <ParseFunction #GN-Entry (e.Name) t.ErrorList e.Tail>;

  t.ErrorList #Entry t.Unexpected e.Tail =
    (/* ничего не генерируем */)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
    t.Unexpected e.Tail;

  t.ErrorList s.Directive e.Tail =
    <ParseNameList
      (/* generated */) t.ErrorList
      <ASTItemFromDirective s.Directive>
      e.Tail
    >;
}

/*
  NameList = Name ',' NameList | Name ';' .
*/

ParseNameList {
  (e.AST) t.ErrorList s.ASTItem s.ScopeClass
  (#TkName s.LnNum e.Name) e.Tail =
    <ParseNameList-Tail
      (e.AST <Wrap s.ASTItem s.LnNum s.ScopeClass e.Name>)
      t.ErrorList s.ASTItem s.ScopeClass e.Tail
    >;

  (e.AST) t.ErrorList s.ASTItem s.ScopeClass
  t.Unexpected e.Tail =
    (e.AST)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
    t.Unexpected e.Tail;
}

Wrap {
  #Ident s.LnNum s.ScopeClass e.Name = (#Ident s.LnNum e.Name);

  s.ASTItem s.LnNum s.ScopeClass e.Name =
    (s.ASTItem s.LnNum s.ScopeClass e.Name);
}

ParseNameList-Tail {
  (e.AST) t.ErrorList s.ASTItem s.ScopeClass (#TkComma s.LnNum) e.Tail =
    <ParseNameList (e.AST) t.ErrorList s.ASTItem s.ScopeClass e.Tail>;

  (e.AST) t.ErrorList s.ASTItem s.ScopeClass (#TkSemicolon s.LnNum) e.Tail =
    (e.AST) t.ErrorList e.Tail;

  (e.AST) t.ErrorList s.ASTItem s.ScopeClass t.Unexpected e.Tail =
    (e.AST)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'comma or semicolon'>
    t.Unexpected e.Tail;
}

/*
  Function = Name '{' Sentence* '}'
*/

ParseFunction {
  s.ScopeClass (e.Name) t.ErrorList (#TkOpenBlock s.LnNum) e.Tail =
    <Fetch
      <ParseBlock t.ErrorList <Cntx-Create> e.Tail>
      {
        (e.Sentences) t.ErrorList^ t.Context e.Tail^ =
          <Cntx-Destroy t.Context>
          ((#Function s.LnNum s.ScopeClass (e.Name) e.Sentences))
          t.ErrorList e.Tail;
      }
    >;

  s.ScopeClass (e.Name) t.ErrorList t.Unexpected e.Tail =
    (/* ничего не сгенерировали */)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'open brace'>
    t.Unexpected e.Tail;
}

/*
  <ParseBlock t.ErrorList t.Context e.Tokens>
    == t.FunctionBody t.ErrorList t.Context e.Tokens

  t.FunctionBody ::= (((e.Pattern) (e.Result))*)

  Открывающая скобка должна отсутствовать во входном потоке.
*/
ParseBlock {
  t.ErrorList t.Context e.Tokens =
    <DoParseBlock
      t.ErrorList
      <Cntx-PushScope t.Context>
      (/* sentences */) e.Tokens
    >;
}

DoParseBlock {
  t.ErrorList t.Context (e.Sentences) (#TkCloseBlock s.LnNumber) e.Tail =
    (e.Sentences) t.ErrorList <Cntx-PopScope t.Context> e.Tail;

  t.ErrorList t.Context (e.Sentences) (#TkEOF s.LnNum) e.Tail =
    (e.Sentences)
    <EL-AddErrorAt
      t.ErrorList s.LnNum 'Unexpected EOF, expected "}"'
    >
    <Cntx-PopScope t.Context>
    (#TkEOF s.LnNum) e.Tail;

  t.ErrorList t.Context (e.Sentences) e.Tokens =
    <DoParseBlock
      <ParseSentence
        t.ErrorList t.Context
        (e.Sentences) e.Tokens
      >
    >;
}

ParseSentence {
  t.ErrorList t.Context (e.Sentences) e.Tokens =
    <Fetch
      <ParsePattern
        t.ErrorList t.Context ( /* мультискобки */ )
        ( /* просканировано */ ) e.Tokens
      >
      <Seq
        {
          t.ErrorList^ t.Context^
          (e.Pattern) (#TkReplace s.LnNum) e.Tokens^ =
            t.ErrorList t.Context
            (e.Pattern) e.Tokens;

          t.ErrorList^ t.Context^
          (e.Pattern) (s.NextResultTerm s.LnNum e.Info) e.Tokens^ =
            <EL-AddErrorAt
              t.ErrorList s.LnNum 'Incorrect pattern termination'
            >
            t.Context
            (e.Pattern) e.Tokens;
        }
        {
          t.ErrorList^ t.Context^
          (e.Pattern) e.Tokens^ =
            (e.Pattern)
            <ParseResult
              t.ErrorList t.Context
              ( /* мультискобки */ ) ( /* просканировано */ ) e.Tokens
            >;
        }
        {
          (e.Pattern) t.ErrorList^ t.Context^
          (e.Result) (#TkSemicolon s.LnNum) e.Tokens^ =
            t.ErrorList
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result))) e.Tokens;

          (e.Pattern) t.ErrorList^ t.Context^
          (e.Result) (#TkCloseBlock s.LnNum) e.Tokens^ =
            <EL-AddErrorAt t.ErrorList s.LnNum 'Missed semicolon'>
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result)))
            (#TkCloseBlock s.LnNum) e.Tokens;

          (e.Pattern) t.ErrorList^ t.Context^
          (e.Result) t.Unexpected e.Tokens^ =
            <EL-AddUnexpected t.ErrorList t.Unexpected 'semicolon'>
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result)))
            t.Unexpected e.Tokens;
        }
      >
    >;
}

ParsePattern {
  // Обработка литералов атомов

  t.ErrorList t.Context (e.MultiBrackets)
  (e.Scanned) (#TkChar s.LnNum s.Char) e.Tail =
    <ParsePattern
      t.ErrorList t.Context (e.MultiBrackets)
      (e.Scanned (#TkChar s.Char)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBrackets)
  (e.Scanned) (#TkNumber s.LnNum s.Number) e.Tail =
    <ParsePattern
      t.ErrorList t.Context (e.MultiBrackets)
      (e.Scanned (#TkNumber s.Number)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBrackets)
  (e.Scanned) (#TkName s.LnNum e.Name) e.Tail =
    <ParsePattern
      t.ErrorList
      t.Context (e.MultiBrackets)
      (e.Scanned (#TkName s.LnNum e.Name)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBrackets)
  (e.Scanned)
  (#TkIdentMarker s.LnNumMarker) (#TkName s.LnNumName e.Name) e.Tail =
    <ParsePattern
      t.ErrorList t.Context (e.MultiBrackets)
      (e.Scanned (#TkIdentifier e.Name)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBrackets)
  (e.Scanned) (#TkIdentMarker s.LnNum) e.Tail =
    <ParsePattern
      <EL-AddErrorAt t.ErrorList s.LnNum 'Missed name after ''#'''>
      t.Context (e.MultiBrackets)
      (e.Scanned) e.Tail
    >;

  // Переменные

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned)
  (#TkVariable s.LnNumVar s.Mode e.Index)
  (#TkRedefinition s.LnNumRedef)
  e.Tail =
    <Fetch
      <CheckAddVariable
        t.ErrorList t.Context
        Cntx-AddNewVariable s.LnNumVar s.Mode e.Index
      >
      {
        t.ErrorList^ t.Context^ s.Mode^ =
          <ParsePattern
            t.ErrorList t.Context (e.MultiBracket)
            (e.Scanned (#TkNewVariable s.LnNumVar s.Mode e.Index)) e.Tail
          >;
      }
    >;

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned) (#TkVariable s.LnNum s.Mode e.Index) e.Tail =
    <Fetch
      <CheckAddVariable
        t.ErrorList t.Context
        Cntx-AddVariable s.LnNum s.Mode e.Index
      >
      {
        t.ErrorList^ t.Context^ s.Mode^ =
          <ParsePattern
            t.ErrorList t.Context (e.MultiBracket)
            (e.Scanned (#TkVariable s.LnNum s.Mode e.Index)) e.Tail
          >;
      }
    >;

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned) (#TkRedefinition s.LnNum) e.Tail =
    <ParsePattern
      <EL-AddErrorAt t.ErrorList s.LnNum 'Naked redefinition sign'>
      t.Context (e.MultiBracket)
      (e.Scanned) e.Tail
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту" закрывающую скобку,
    считаем, что "та" была пропущена. Если имеем непарную открывающую скобку,
    считаем, что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned) (# Open s.LnNum #TkOpenBracket) e.Tail =
    <ParsePattern
      t.ErrorList t.Context
      (
        e.MultiBracket
        (#Brackets s.LnNum #TkCloseBracket e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned)
  (# Open s.LnNumBracket #TkOpenADT) (#TkName s.LnNumName e.Name) e.Tail =
    <ParsePattern
      t.ErrorList
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets s.LnNumBracket #TkCloseADT e.Scanned)
      )
      (s.LnNumName (e.Name)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned) (# Open s.LnNum #TkOpenADT) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList s.LnNum 'Expected name after "["'
      >
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets s.LnNum #TkCloseADT e.Scanned)
      )
      (s.LnNum (#UnnamedADT)) e.Tail
    >;

  t.ErrorList t.Context (e.MultiBracket)
  (e.Scanned) (# Open s.LnNum #TkOpenCall) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList s.LnNum 'Unexpected call bracket in pattern expression'
      >
      t.Context
      (
        e.MultiBracket
        (#Brackets s.LnNum #TkCloseCall e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList t.Context
  (
    e.MultiBracket
    (s.BracketsSign s.InnerLnNum s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# Close s.ClosedLnNum s.CloseBracket) e.Tail =
    <ParsePattern
      t.ErrorList t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList t.Context
  (
    e.MultiBracket
    (s.BracketsSign s.InnerLnNum s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# Close s.LnNum s.OtherCloseBracket) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList s.InnerLnNum
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      (# Close s.LnNum s.OtherCloseBracket) e.Tail
    >;

  t.ErrorList t.Context ()
  (e.Scanned) (# Close s.LnNum s.CloseBracket) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList s.LnNum
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      t.Context ()
      (e.Scanned) e.Tail
    >;

  t.ErrorList t.Context
  (
    e.MultiBracket
    (s.BracketsSign s.InnerLnNum s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList s.InnerLnNum
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList t.Context () (e.Pattern) e.Tail =
    t.ErrorList t.Context (e.Pattern)  e.Tail;
}

InvalidModeVariableError {
  t.ErrorList s.LnNum s.OldMode s.NewMode e.Index =
    <EL-AddErrorAt
      t.ErrorList s.LnNum
      'Variables ' s.OldMode '.' e.Index ' and '
      s.NewMode '.' e.Index ' has different modes'
    >;
}

CheckAddVariable {
  t.ErrorList t.Context
  s.FnContextModifier s.LnNum s.Mode e.Index =
    <Fetch
      <s.FnContextModifier t.Context s.Mode e.Index>
      {
        t.Context^ # Success =
          t.ErrorList t.Context s.Mode;

        t.Context^ # InvalidMode s.OldMode =
          <InvalidModeVariableError
            t.ErrorList s.LnNum s.OldMode s.Mode e.Index
          >
          t.Context s.OldMode;

        t.Context^ # AlreadyBounded =
          <EL-AddErrorAt
            t.ErrorList s.LnNum
            'Variable hide operator^ must be applied to first occurence of '
            'variable ' s.Mode '.' e.Index
          >
          t.Context s.Mode;
      }
    >;
}

StrFromBracket {
  #TkOpenBracket = '('; #TkCloseBracket = ')';
  #TkOpenCall = '<'; #TkCloseCall = '>';
  #TkOpenADT = '['; #TkCloseADT = ']';
  #Brackets = '(';
  #ADT-Brackets = '[';
  #CallBrackets = '<';
}

/*
  <ParseResult
    t.ErrorList t.Context
    (e.MultiBracket) (e.Scanned) e.Tail
  >
*/
ParseResult {
  // Обработка литералов атомов

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkChar s.LnNum s.Char) e.Tail =
    <ParseResult
      t.ErrorList t.Context
      (e.MultiBracket) (e.Scanned (#TkChar s.Char)) e.Tail
    >;

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkNumber s.LnNum s.Number) e.Tail =
    <ParseResult
      t.ErrorList t.Context
      (e.MultiBracket) (e.Scanned (#TkNumber s.Number)) e.Tail
    >;

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkName s.LnNum e.Name) e.Tail =
    <ParseResult
      t.ErrorList
      t.Context
      (e.MultiBracket) (e.Scanned (#TkName s.LnNum e.Name)) e.Tail
    >;

  t.ErrorList t.Context
  (e.MultiBracket)
  (e.Scanned)
  (#TkIdentMarker s.MarkerLnNum) (#TkName s.NameLnNum e.Name) e.Tail =
    <ParseResult
      t.ErrorList t.Context
      (e.MultiBracket) (e.Scanned (#TkIdentifier e.Name)) e.Tail
    >;

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkIdentMarker s.LnNum) e.Tail =
    <ParseResult
      <EL-AddErrorAt t.ErrorList s.LnNum 'Missed name after "#"'>
      t.Context
      (e.MultiBracket) (e.Scanned) e.Tail
    >;

  // Переменные

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkVariable s.LnNum s.Mode e.Index) e.Tail =
    <Fetch
      <CheckUseVariable t.ErrorList t.Context s.LnNum s.Mode e.Index>
      {
        t.ErrorList^ t.Context^ s.Mode^ =
          <ParseResult
            t.ErrorList t.Context
            (e.MultiBracket)
            (e.Scanned (#TkVariable s.LnNum s.Mode e.Index)) e.Tail
          >;

        t.ErrorList^ t.Context^ =
          <ParseResult
            t.ErrorList t.Context
            (e.MultiBracket) (e.Scanned) e.Tail
          >;
      }
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту" закрывающую скобку,
    считаем, что "та" была пропущена. Если имеем непарную открывающую скобку,
    считаем, что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  // Хитро обрабатываются АДТ-скобки, т.к. требуется, чтобы после них было имя.

  t.ErrorList t.Context
  (e.MultiBracket)
  (e.Scanned)
  (# Open s.LnNumADT #TkOpenADT) (#TkName s.LnNumName e.Name) e.Tail =
    <ParseResult
      t.ErrorList
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets s.LnNumADT #TkCloseADT e.Scanned)
      )
      (s.LnNumName (e.Name)) e.Tail
    >;

  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (# Open s.LnNum #TkOpenADT) e.Tail =
    <ParseResult
      <EL-AddErrorAt
        t.ErrorList s.LnNum 'Expected name after "["'
      >
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets s.LnNum #TkCloseADT e.Scanned)
      )
      (s.LnNum (#UnnamedADT)) e.Tail
    >;

  // Структурные и угловые скобки обрабатываются одинаково
  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (# Open s.LnNum s.OpenBracket) e.Tail =
    <ParseResult
      t.ErrorList t.Context
      (
        e.MultiBracket
        (
          <Fetch
            s.OpenBracket
            {
              #TkOpenBracket = #Brackets;
              #TkOpenCall = #CallBrackets;
            }
          >
          s.LnNum
          <Fetch
            s.OpenBracket
            {
              #TkOpenBracket = #TkCloseBracket;
              #TkOpenCall = #TkCloseCall;
            }
          >
          e.Scanned
        )
      )
      () e.Tail
    >;

  // Правильная закрывающая скобка
  t.ErrorList t.Context
  (
    e.MultiBracket
    (s.BracketsSign s.OpenLnNum s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# Close s.LnNum s.CloseBracket) e.Tail =
    <ParseResult
      t.ErrorList t.Context
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  // Лямбда-функция
  t.ErrorList t.Context
  (e.MultiBracket) (e.Scanned) (#TkOpenBlock s.LnNum) e.Tail =
    <Fetch
      <ParseBlock t.ErrorList t.Context e.Tail>
      {
        (e.Sentences) t.ErrorList^ t.Context^ e.Tail^ =
          <ParseResult
            t.ErrorList t.Context
            (e.MultiBracket)
            (e.Scanned (#Closure e.Sentences))
            e.Tail
          >;
      }
    >;

  // Неправильная закрывающая скобка или закрывающая скобка отсутствует
  t.ErrorList t.Context
  (
    e.MultiBracket
    (s.BracketsSign s.OpenLnNum s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <ParseResult
      <EL-AddErrorAt
        t.ErrorList s.OpenLnNum
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.Context
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList t.Context
  () (e.Scanned) (# Close s.LnNum s.CloseBracket) e.Tail =
    <ParseResult
      <EL-AddErrorAt
        t.ErrorList s.LnNum
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      t.Context
      () (e.Scanned) e.Tail
    >;

  t.ErrorList t.Context () (e.Result) e.Tail =
    t.ErrorList t.Context (e.Result) e.Tail;
}

CheckUseVariable {
  t.ErrorList t.Context s.LnNum s.Mode e.Index =
    <Fetch
      <Cntx-CheckVariable t.Context s.Mode e.Index>
      {
        t.Context^ # ExistVariable =
          t.ErrorList t.Context s.Mode;

        t.Context^ # InvalidMode s.OldMode =
          <InvalidModeVariableError
            t.ErrorList s.LnNum s.OldMode s.Mode e.Index
          >
          t.Context s.OldMode;

        t.Context^ # NotFound =
          <EL-AddErrorAt
            t.ErrorList s.LnNum 'Variable ' s.Mode '.' e.Index ' not found'
          >
          t.Context;
      }
    >;
}
