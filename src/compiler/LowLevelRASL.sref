//FROM LibraryEx
$EXTERN Map, Fetch, Max, Seq, Inc, Compare, MapReduce, Reduce, DelAccumulator;

//FROM Library
$EXTERN Add, StrFromInt;

//FROM Hash
$EXTERN HashLittle2-Chars;

//FROM DisplayName
$EXTERN DisplayName;

/**
  <LowLevelRASL s.GenMode e.RASLAST>
    == e.RASL (e.NativeRASL)

  s.GenMode ::= #OnlyDirect | #OnlyInterpret
*/
$ENTRY LowLevelRASL {
  s.GenMode (e.SrcName) e.ProgramElements =
    <Fetch
      e.ProgramElements
      <Seq
        (MapReduce
          CalcDigest
          // Числа выбраны случайно: hexdump /dev/urandom
          (<HashLittle2-Chars 4001567069 291363191 e.SrcName>)
        )
        {
          (s.Hash1 s.Hash2) e.Items =
            <Map (SetCookies s.Hash1 s.Hash2) e.Items>;
        }
        (Map
          {
            (#Function (e.Name) e.Commands) =
              (#Function (e.Name) <AddSavers e.Commands>);

            t.OtherItem = t.OtherItem;
          }
        )
        (Reduce
          {
            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#Function (e.Name) e.Commands) =
              <Fetch
                s.GenMode
                {
                  #OnlyInterpret =
                    (
                      (e.RASL-Declarations (#CmdExtern e.Name))
                      (
                        e.RASL-Definitions
                        <Function-Interpret (e.Name) e.Commands>
                      )
                      (e.Native-Declarations (#CmdExtern e.Name))
                      (e.Native-Definitions)
                    );

                  #OnlyDirect =
                    (
                      (e.RASL-Declarations (#CmdExtern e.Name))
                      (e.RASL-Definitions)
                      (e.Native-Declarations (#CmdExtern e.Name))
                      (
                        e.Native-Definitions
                        <Function-Direct (e.Name) e.Commands>
                      )
                    );
                }
              >;

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdNativeFunction (e.Name) t.SrcPos e.Code) =
              (
                (e.RASL-Declarations (#CmdExtern e.Name))
                (e.RASL-Definitions)
                (e.Native-Declarations (#CmdExtern e.Name))
                (
                  e.Native-Definitions
                  (#CmdFnStart e.Name)
                  (#CmdEmitNativeCode t.SrcPos e.Code)
                  (#CmdFnEnd)
                  (#CmdNativeFuncDescr e.Name)
                )
              );

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdEnum e.Name) =
              (
                (e.RASL-Declarations (#CmdEnumDescr e.Name))
                (e.RASL-Definitions)
                (e.Native-Declarations (#CmdExtern e.Name))
                (e.Native-Definitions)
              );

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdSwap e.Name) =
              (
                (e.RASL-Declarations (#CmdSwapDescr e.Name))
                (e.RASL-Definitions)
                (e.Native-Declarations (#CmdExtern e.Name))
                (e.Native-Definitions)
              );

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdDeclaration e.Name) =
              (
                (e.RASL-Declarations (#CmdExtern e.Name))
                (e.RASL-Definitions)
                (e.Native-Declarations (#CmdDeclaration e.Name))
                (e.Native-Definitions)
              );

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdDefineIdent e.Name) =
              (
                (e.RASL-Declarations)
                (e.RASL-Definitions)
                (e.Native-Declarations (#CmdDefineIdent e.Name))
                (e.Native-Definitions)
              );

            (
              (e.RASL-Declarations)
              (e.RASL-Definitions)
              (e.Native-Declarations)
              (e.Native-Definitions)
            )
            (#CmdEmitNativeCode t.SrcPos e.Code) =
              (
                (e.RASL-Declarations)
                (e.RASL-Definitions)
                (e.Native-Declarations)
                (
                  e.Native-Definitions
                  (#CmdEmitNativeCode t.SrcPos e.Code)
                )
              );
          }
          (
            (/* объявления для генерации RASL’а */)
            (/* определения для генерации RASL’а */)
            (/* объявления для генерации нативного кода */)
            (/* определения для генерации нативного кода */)
          )
        )
        {
          (
            (e.RASL-Declarations)
            (e.RASL-Definitions)
            (e.Native-Declarations)
            (e.Native-Definitions)
          ) =
            <Fetch
              <OutlineConstants
                (0 /* функции */)
                (0 /* идентификаторы */)
                (0 /* большие числа */)
                (0 /* строки */)
                e.RASL-Definitions
              >
              {
                (#CmdFuncArray e.Functions) e.RASL-Commands =
                  e.RASL-Declarations
                  (#CmdSeparator)
                  (#CmdFuncArray
                    <DelAccumulator
                      <MapReduce
                        {
                          (
                            e.Decls-B
                            (s.Type e.FuncName #Hash s.Hash1 s.Hash2)
                            e.Decls-E
                          )
                          (e.FuncName) =
                            (e.Decls-B e.Decls-E)
                            (e.FuncName #Hash s.Hash1 s.Hash2);

                          (e.Decls) (#CreateClosure) =
                            (e.Decls) (#CreateClosure);
                        }
                        (e.RASL-Declarations)
                        e.Functions
                      >
                    >
                  )
                  e.RASL-Commands;
              }
            >
            (
              <Fetch
                e.Native-Definitions
                {
                  /* нет нативных определений */ =
                    /* нет нативного RASL’а */;

                  e.Native-Definitions^ =
                    e.Native-Declarations
                    (#CmdSeparator)
                    e.Native-Definitions;
                }
              >
            );
        }
      >
    >;
}

CalcDigest {
  (s.Hash1 s.Hash2) (#Function s.ScopeClass (e.Name) e.Commands) =
    (<HashFuncName s.Hash1 s.Hash2 s.ScopeClass e.Name 'R'>)
    (#Function s.ScopeClass (e.Name) e.Commands);

  (s.Hash1 s.Hash2)
  (#CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code) =
    (<HashFuncName s.Hash1 s.Hash2 s.ScopeClass e.Name 'N'>)
    (#CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code);

  (s.Hash1 s.Hash2) (#CmdEnum s.ScopeClass e.Name) =
    (<HashFuncName s.Hash1 s.Hash2 s.ScopeClass e.Name 'E'>)
    (#CmdEnum s.ScopeClass e.Name);

  (s.Hash1 s.Hash2) (#CmdSwap s.ScopeClass e.Name) =
    (<HashFuncName s.Hash1 s.Hash2 s.ScopeClass e.Name 'S'>)
    (#CmdSwap s.ScopeClass e.Name);

  (s.Hash1 s.Hash2) (#CmdDeclaration s.ScopeClass e.Name) =
    (<HashFuncName s.Hash1 s.Hash2 s.ScopeClass e.Name 'D'>)
    (#CmdDeclaration s.ScopeClass e.Name);

  (s.Hash1 s.Hash2) (#CmdDefineIdent e.Name) =
    (s.Hash1 s.Hash2) (#CmdDefineIdent e.Name);

  (s.Hash1 s.Hash2) (#CmdEmitNativeCode t.SrcPos e.Code) =
    (s.Hash1 s.Hash2) (#CmdEmitNativeCode t.SrcPos e.Code);
}

HashFuncName {
  s.Hash1 s.Hash2 s.ScopeClass e.Name s.Sign =
    <HashLittle2-Chars
      s.Hash1 s.Hash2
      <CharFromScopeClass s.ScopeClass> <DisplayName e.Name> s.Sign
    >;
}

CharFromScopeClass {
  #GN-Entry = 'E';
  #GN-Local = 'L';
}

SetCookies {
  s.Hash1 s.Hash2
  (#Function s.ScopeClass (e.Name) e.Commands) =
    (#Function
      (<MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>)
      e.Commands
    );

  s.Hash1 s.Hash2
  (#CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code) =
    (#CmdNativeFunction
      (<MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>)
      t.SrcPos e.Code
    );

  s.Hash1 s.Hash2 (#CmdEnum s.ScopeClass e.Name) =
    (#CmdEnum <MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>);

  s.Hash1 s.Hash2 (#CmdSwap s.ScopeClass e.Name) =
    (#CmdSwap <MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>);

  s.Hash1 s.Hash2 (#CmdDeclaration s.ScopeClass e.Name) =
    (#CmdDeclaration
      <MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>
    );

  s.Hash1 s.Hash2 (#CmdDefineIdent e.Name) =
    (#CmdDefineIdent e.Name);

  s.Hash1 s.Hash2 (#CmdEmitNativeCode t.SrcPos e.Code) =
    (#CmdEmitNativeCode t.SrcPos e.Code);
}

MingleName {
  #GN-Entry e.Name s.Hash1 s.Hash2 = e.Name #Hash 0 0;
  #GN-Local e.Name s.Hash1 s.Hash2 = e.Name #Hash s.Hash1 s.Hash2;
}

AddSavers {
  (#CmdIssueMem s.Memory) e.Commands =
    <DoAddSavers
      s.Memory
      (/* alive vars */) (/* frozen vars */)
      (/* scanned */) e.Commands
    >;
}

DoAddSavers {
  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdInitB0) e.Commands =
    <DoAddSavers
      s.ContextTop (e.Alive 0) (e.Frozen)
      (e.Scanned (#CmdInitB0)) e.Commands
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdInitB0-Lite) e.Commands =
    <DoAddSavers
      s.ContextTop (e.Alive 0) (e.Frozen)
      (e.Scanned (#CmdInitB0-Lite)) e.Commands
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdCallSave #AlgLeft s.BracketNumber s.InnerNum e.Name) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive s.InnerNum) (e.Frozen)
            (
              e.Scanned
              e.Saver
              (#CmdCallSave #AlgLeft s.BracketNumber s.InnerNum e.Name)
            )
            e.Commands
          >;
      }
    >;

  /*
    Эти предложения корректно обрабатывает и #AlgTerm:
    s.BracketNumber, который в этом случае будет номером t-переменной,
    просто не найдётся в таблицах e.Alive и e.Frozen, а функция
    CheckSaved вернёт без изменения свой аргумент.
  */
  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdBrackets s.Direction s.BracketNumber s.InnerNum) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive s.InnerNum) (e.Frozen)
            (
              e.Scanned
              e.Saver
              (#CmdBrackets s.Direction s.BracketNumber s.InnerNum)
            )
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdBracketsSave s.Direction s.BracketNumber s.InnerNum) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive s.InnerNum) (e.Frozen)
            (
              e.Scanned
              e.Saver
              (#CmdBracketsSave s.Direction s.BracketNumber s.InnerNum)
            )
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdADT s.Direction s.BracketNumber s.InnerNum e.Name) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive s.InnerNum) (e.Frozen)
            (
              e.Scanned
              e.Saver
              (#CmdADT s.Direction s.BracketNumber s.InnerNum e.Name)
            )
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdADTSave s.Direction s.BracketNumber s.InnerNum e.Name) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive s.InnerNum) (e.Frozen)
            (
              e.Scanned
              e.Saver
              (#CmdADTSave s.Direction s.BracketNumber s.InnerNum e.Name)
            )
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdComment e.Comment) e.Commands =
    <DoAddSavers
      s.ContextTop (e.Alive) (e.Frozen)
      (
        e.Scanned
        (#CmdComment <FixupFrozenComment (e.Frozen) e.Comment>)
      )
      e.Commands
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdRepeated s.Direction s.BracketNumber 'e' s.VarNumber s.SampleNum)
  e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <Fetch
            <CheckFrozen e.Frozen s.SampleNum>
            {
              s.SampleNum^ =
                <DoAddSavers
                  s.ContextTop (e.Alive) (e.Frozen)
                  (
                    e.Scanned
                    e.Saver
                    (#CmdRepeated
                      s.Direction s.BracketNumber 'e' s.VarNumber s.SampleNum
                    )
                  )
                  e.Commands
                >;
            }
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned)
  (#CmdOpenELoop #AlgLeft s.BracketNumber s.VarNumber e.SubCommands)
  /* обычно, последняя команда, но после неё может следовать #CmdFail */
  e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <Fetch
            <DoAddSavers
              s.ContextTop () (e.Frozen <Freeze e.Alive>)
              () e.SubCommands
            >
            {
              (#CmdIssueMem s.ContextTop^) e.SubCommands^ =
                (#CmdIssueMem s.ContextTop)
                e.Scanned
                e.Saver
                (#CmdOpenELoop
                  #AlgLeft s.BracketNumber s.VarNumber e.SubCommands
                )
                <Fetch
                  e.Commands
                  {
                    /* пусто */ = /* пусто */;
                    (#CmdFail) = (#CmdFail);
                  }
                >;
            }
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (s.MatchCommand #AlgLeft s.BracketNumber e.Info) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive) (e.Frozen)
            (e.Scanned e.Saver (s.MatchCommand #AlgLeft s.BracketNumber e.Info))
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (s.MatchCommand #AlgRight s.BracketNumber e.Info) e.Commands =
    <Fetch
      <CheckSaved s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber>
      {
        s.ContextTop^ (e.Alive^) (e.Frozen^) s.BracketNumber^ e.Saver =
          <DoAddSavers
            s.ContextTop (e.Alive) (e.Frozen)
            (
              e.Scanned e.Saver
              (s.MatchCommand #AlgRight s.BracketNumber e.Info)
            )
            e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdCopyVar 'e' s.Target s.Sample) e.Commands =
    <Fetch
      <CheckFrozen e.Frozen s.Sample>
      {
        s.Sample^ =
          <DoAddSavers
            s.ContextTop (e.Alive) (e.Frozen)
            (e.Scanned (#CmdCopyVar 'e' s.Target s.Sample)) e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdInsertVar 'e' s.Offset) e.Commands =
    <Fetch
      <CheckFrozen e.Frozen s.Offset>
      {
        s.Offset^ =
          <DoAddSavers
            s.ContextTop (e.Alive) (e.Frozen)
            (e.Scanned (#CmdInsertVar 'e' s.Offset)) e.Commands
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen)
  (e.Scanned) (#CmdSentence e.SubCommands) e.Commands =
    <Fetch
      <DoAddSavers
        s.ContextTop () (e.Frozen <Freeze e.Alive>)
        () e.SubCommands
      >
      {
        (#CmdIssueMem s.InnerContextTop) e.SubCommands^ =
          <Fetch
            <DoAddSavers
              s.ContextTop (e.Alive) (e.Frozen)
              (e.Scanned (#CmdSentence e.SubCommands))
              e.Commands
            >
            {
              (#CmdIssueMem s.OuterContextTop) e.Scanned^ =
                (#CmdIssueMem <Max s.InnerContextTop s.OuterContextTop>)
                e.Scanned;
            }
          >;
      }
    >;

  s.ContextTop (e.Alive) (e.Frozen) (e.Scanned) t.OtherCommand e.Commands =
    <DoAddSavers
      s.ContextTop (e.Alive) (e.Frozen)
      (e.Scanned t.OtherCommand) e.Commands
    >;

  s.ContextTop (e.Alive) (e.Frozen) (e.Scanned) /* всё */ =
    (#CmdIssueMem s.ContextTop) e.Scanned;
}

CheckSaved {
  s.ContextTop
  (e.Alive-B s.BracketNumber e.Alive-E) (e.Frozen) s.BracketNumber =
    s.ContextTop
    (e.Alive-B s.BracketNumber e.Alive-E) (e.Frozen) s.BracketNumber;

  s.ContextTop (e.Alive) (e.Frozen) s.BracketNumber =
    <CheckSaved-Fronen
      s.ContextTop (e.Alive) () (e.Frozen) s.BracketNumber
    >;
}

CheckSaved-Fronen {
  s.ContextTop (e.Alive) (e.ScannedFrozen)
  (e.Frozen-B (s.BracketNumber #Frozen) e.Frozen-E) s.BracketNumber =
    <Add s.ContextTop 2>
    (e.Alive s.ContextTop)
    (e.ScannedFrozen e.Frozen-B (s.BracketNumber s.ContextTop) e.Frozen-E)
    s.ContextTop (#CmdSave s.BracketNumber s.ContextTop);

  s.ContextTop (e.Alive) (e.ScannedFrozen)
  (e.Frozen-B (s.BracketNumber s.NewNumber) e.Frozen-E) s.BracketNumber =
    <CheckSaved-Fronen
      s.ContextTop (e.Alive)
      (e.ScannedFrozen e.Frozen-B (s.BracketNumber s.NewNumber))
      (e.Frozen-E) s.NewNumber
    >;

  s.ContextTop (e.Alive) (e.ScannedFrozen) (e.Frozen) s.BracketNumber =
    s.ContextTop (e.Alive) (e.ScannedFrozen e.Frozen) s.BracketNumber;
}

FixupFrozenComment {
  (e.Frozen) e.Comment-B #Offset s.Num e.Comment-E =
    e.Comment-B
    <Fetch
      <CheckFrozen e.Frozen s.Num>
      {
        s.Num = <StrFromInt s.Num>;
        s.Renum = <StrFromInt s.Renum> '(' <StrFromInt s.Num> ')';
      }
    >
    <FixupFrozenComment (e.Frozen) e.Comment-E>;

  (e.Frozen) e.Comment = e.Comment;
}

CheckFrozen {
  e.Frozen-B (s.BracketNumber #Frozen) e.Frozen-E s.BracketNumber =
    s.BracketNumber;

  e.Frozen-B (s.BracketNumber s.NewNumber) e.Frozen-E s.BracketNumber =
    <CheckFrozen e.Frozen-E s.NewNumber>;

  e.Frozen s.BracketNumber = s.BracketNumber;
}

Freeze {
  e.BracketNumbers =
    <Map { s.Number = (s.Number #Frozen); } e.BracketNumbers>;
}

Function-Interpret {
  (e.Name) e.FunctionBody =
    <Fetch
      e.FunctionBody
      <Seq
        ConvertInterpretCommands
        PrepareOpenEStack
        {
          e.FunctionBody^ =
            (#CmdInitRAA e.Name)
            (#CmdProfileFunction)
            (#CmdLoadConstants)
            e.FunctionBody
            (#CmdFinRAA);
        }
        {
          e.FunctionBody^ =
            e.FunctionBody
            (#CmdInterpretFuncDescr e.Name);
        }
      >
    >;
}

ConvertInterpretCommands {
  e.Commands = <Map ConvertOneInterpretCommand e.Commands>;
}

Length-T {
  e.Expr = <DoLength 0 e.Expr>;
}

DoLength {
  s.Count e.Expr (#CmdComment e.Text) =
    <DoLength s.Count e.Expr> (#CmdComment e.Text);

  s.Count e.Expr t.Last = <DoLength <Inc s.Count> e.Expr> t.Last;

  s.Count /* пусто */ = s.Count;
}

ConvertOneInterpretCommand {
  (#CmdNumber s.Direction s.BracketNumber s.Number) =
    <Fetch
      <Compare s.Number 256> {
        '<' = (#CmdNum s.Direction s.BracketNumber s.Number);

        s.Other = (#CmdHugeNum s.Direction s.BracketNumber s.Number);
      }
    >;

  (#CmdNumberSave s.Direction s.BracketNumber s.SaveOffset s.Number) =
    <Fetch
      <Compare s.Number 256> {
        '<' =
          (#CmdNumSave s.Direction s.BracketNumber s.SaveOffset s.Number);

        s.Other =
          (#CmdHugeNumSave s.Direction s.BracketNumber s.SaveOffset s.Number);
      }
    >;

  (#CmdCreateElem s.CreateMode s.ElemNo #ElNumber s.Number) =
    <Fetch
      <Compare s.Number 256> {
        '<' = (#CmdCreateElem s.CreateMode s.ElemNo #ElNumber s.Number);
        s.Other = (#CmdCreateElem s.CreateMode s.ElemNo #ElHugeNumber s.Number);
      }
    >;

  (#CmdUseRes) = /* пусто */;

  (#CmdSentence e.SubCommands) =
    <Fetch
      <Length-T <ConvertInterpretCommands e.SubCommands>>
      {
        s.Length e.SubCommands^ =
          (#CmdOnFailGoTo s.Length)
          e.SubCommands
          (#CmdProfilerStopSentence);
      }
    >;

  (#CmdOpenELoop #AlgLeft s.BracketNum s.VarNumber e.SubCommands) =
    (#CmdEPrepare s.BracketNum s.VarNumber)
    (#CmdEStart s.BracketNum s.VarNumber)
    <ConvertInterpretCommands e.SubCommands>;

  t.Command = t.Command;
}

RemoveNumber { (s.Number e.Name) = (e.Name); }

OutlineConstants {
  (s.NextFunc e.Funcs)
  (s.NextLabel e.Labels)
  (s.NextNumber e.Numbers)
  (s.NextString e.Strings)
  /* пусто */ =
    (#CmdFuncArray <Map RemoveNumber e.Funcs>)
    (#CmdLabelArray <Map RemoveNumber e.Labels>)
    (#CmdNumberArray <Map { (s.Id s.Value) = s.Value; } e.Numbers>)
    (#CmdStringArray <Map RemoveNumber e.Strings>);

  (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdName s.Direction s.BracketNum e.Func) =
    <OutlineConstants
      (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdName s.Direction s.BracketNum s.Num);

  (s.NextFunc e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdName s.Direction s.BracketNum e.Func) =
    <OutlineConstants
      (<Inc s.NextFunc> e.Funcs (s.NextFunc e.Func)) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdName s.Direction s.BracketNum s.NextFunc);

  (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdNameSave s.Direction s.BracketNum s.SaveOffset e.Func) =
    <OutlineConstants
      (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdNameSave s.Direction s.BracketNum s.SaveOffset s.Num);

  (s.NextFunc e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdNameSave s.Direction s.BracketNum s.SaveOffset e.Func) =
    <OutlineConstants
      (<Inc s.NextFunc> e.Funcs (s.NextFunc e.Func)) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdNameSave s.Direction s.BracketNum s.SaveOffset s.NextFunc);

  (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElName e.Func) =
    <OutlineConstants
      (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElName s.Num);

  (s.NextFunc e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElName e.Func) =
    <OutlineConstants
      (<Inc s.NextFunc> e.Funcs (s.NextFunc e.Func)) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElName s.NextFunc);

  (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdADT s.Direction s.BracketNum s.InnerBrackets e.Func) =
    <OutlineConstants
      (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdADT s.Direction s.BracketNum s.InnerBrackets s.Num);

  (s.NextFunc e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdADT s.Direction s.BracketNum s.InnerBrackets e.Func) =
    <OutlineConstants
      (<Inc s.NextFunc> e.Funcs (s.NextFunc e.Func)) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdADT s.Direction s.BracketNum s.InnerBrackets s.NextFunc);

  (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdADTSave s.Direction s.BracketNum s.InnerBrackets e.Func) =
    <OutlineConstants
      (s.NextFunc e.Funcs-B (s.Num e.Func) e.Funcs-E) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdADTSave s.Direction s.BracketNum s.InnerBrackets s.Num);

  (s.NextFunc e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdADTSave s.Direction s.BracketNum s.InnerBrackets e.Func) =
    <OutlineConstants
      (<Inc s.NextFunc> e.Funcs (s.NextFunc e.Func)) (e.Labels) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdADTSave s.Direction s.BracketNum s.InnerBrackets s.NextFunc);

  (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdIdent s.Direction s.BracketNum e.Label) =
    <OutlineConstants
      (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdIdent s.Direction s.BracketNum s.Num);

  (e.Funcs) (s.NextLabel e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdIdent s.Direction s.BracketNum e.Label) =
    <OutlineConstants
      (e.Funcs) (<Inc s.NextLabel> e.Labels (s.NextLabel e.Label)) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdIdent s.Direction s.BracketNum s.NextLabel);

  (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdIdentSave s.Direction s.BracketNum s.SaveOffset e.Label) =
    <OutlineConstants
      (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdIdentSave s.Direction s.BracketNum s.SaveOffset s.Num);

  (e.Funcs) (s.NextLabel e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdIdentSave s.Direction s.BracketNum s.SaveOffset e.Label) =
    <OutlineConstants
      (e.Funcs) (<Inc s.NextLabel> e.Labels (s.NextLabel e.Label)) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdIdentSave s.Direction s.BracketNum s.SaveOffset s.NextLabel);

  (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
  (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElIdent e.Label) =
    <OutlineConstants
      (e.Funcs) (s.NextLabel e.Labels-B (s.Num e.Label) e.Labels-E) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElIdent s.Num);

  (e.Funcs) (s.NextLabel e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElIdent e.Label) =
    <OutlineConstants
      (e.Funcs) (<Inc s.NextLabel> e.Labels (s.NextLabel e.Label)) (e.Numbers)
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElIdent s.NextLabel);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
  (e.Strings)
  e.Commands
  (#CmdHugeNum s.Direction s.BracketNum s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
      (e.Strings)
      e.Commands
    >
    (#CmdHugeNum s.Direction s.BracketNum s.Id);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers) (e.Strings)
  e.Commands
  (#CmdHugeNum s.Direction s.BracketNum s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (<Inc s.NextNumId> e.Numbers (s.NextNumId s.Number))
      (e.Strings)
      e.Commands
    >
    (#CmdHugeNum s.Direction s.BracketNum s.NextNumId);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
  (e.Strings)
  e.Commands
  (#CmdHugeNumSave s.Direction s.BracketNum s.SaveOffset s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
      (e.Strings)
      e.Commands
    >
    (#CmdHugeNumSave s.Direction s.BracketNum s.SaveOffset s.Id);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers) (e.Strings)
  e.Commands
  (#CmdHugeNumSave s.Direction s.BracketNum s.SaveOffset s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (<Inc s.NextNumId> e.Numbers (s.NextNumId s.Number))
      (e.Strings)
      e.Commands
    >
    (#CmdHugeNumSave s.Direction s.BracketNum s.SaveOffset s.NextNumId);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
  (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElHugeNumber s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (s.NextNumId e.Numbers-B (s.Id s.Number) e.Numbers-E)
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElHugeNumber s.Id);

  (e.Funcs) (e.Labels) (s.NextNumId e.Numbers) (e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElHugeNumber s.Number) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (<Inc s.NextNumId> e.Numbers (s.NextNumId s.Number))
      (e.Strings)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElHugeNumber s.NextNumId);

  (e.Funcs) (e.Labels) (e.Numbers)
  (s.NextStringId e.Strings-B (s.Id e.String) e.Strings-E)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElString e.String) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (e.Numbers)
      (s.NextStringId e.Strings-B (s.Id e.String) e.Strings-E)
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElString s.Id);

  (e.Funcs) (e.Labels) (e.Numbers) (s.NextStringId e.Strings)
  e.Commands
  (#CmdCreateElem s.CreateMode s.ElemNo #ElString e.String) =
    <OutlineConstants
      (e.Funcs) (e.Labels) (e.Numbers)
      (<Inc s.NextStringId> e.Strings (s.NextStringId e.String))
      e.Commands
    >
    (#CmdCreateElem s.CreateMode s.ElemNo #ElString s.NextStringId);

  (e.Funcs) (e.Labels) (e.Numbers) (e.Strings)
  e.Commands
  t.OtherCommand =
    <OutlineConstants (e.Funcs) (e.Labels) (e.Numbers) (e.Strings) e.Commands>
    t.OtherCommand;
}

PrepareOpenEStack {
  e.Commands =
    <MapReduce
      {
        (#CmdReserveStack s.Depth) (#CmdEStart e.VarInfo) =
          (#CmdReserveStack <Inc s.Depth>) (#CmdEStart e.VarInfo);

        (#CmdReserveStack s.Depth) (#CmdOnFailGoTo s.Offset) =
          (#CmdReserveStack <Inc s.Depth>) (#CmdOnFailGoTo s.Offset);

        (#CmdReserveStack s.Depth) t.OtherCommand =
          (#CmdReserveStack s.Depth) t.OtherCommand;
      }
      (#CmdReserveStack 0)
      e.Commands
    >;
}

Function-Direct {
  (e.Name) e.Body =
    (#CmdFnStart e.Name)
    (#CmdProfileFunction)
    <Function-DirectRec e.Body>
    (#CmdFnEnd)
    (#CmdNativeFuncDescr e.Name);
}

Function-DirectRec {
  (#CmdSentence e.SubCommands) e.Tail =
    (#CmdStartSentence)
    <Function-DirectRec e.SubCommands>
    (#CmdEndSentence)
    <Function-DirectRec e.Tail>;

  (#CmdOpenELoop #AlgLeft s.BracketNum s.VarNumber e.SubCommands) e.Tail =
    (#CmdOpenedE-Start #AlgLeft s.BracketNum s.VarNumber)
    <Function-DirectRec e.SubCommands>
    (#CmdOpenedE-End #AlgLeft s.BracketNum s.VarNumber)
    <Function-DirectRec e.Tail>;

  t.Command e.Tail = t.Command <Function-DirectRec e.Tail>;

  /* пусто */ = /* пусто */;
}
