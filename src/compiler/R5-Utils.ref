/**
  <ArgList> == (e.Arg)*
*/
$ENTRY R5-ArgList {
  = <DoArgList 1 <Arg 0>>;
}

DoArgList {
  s.N /* пусто */ = /* пусто */;
  s.N e.Arg =
    (e.Arg) <DoArgList <R5-Inc s.N> <Arg s.N>>;
}


/**
  <Inc e.Num> == e.Num+1
*/
$ENTRY R5-Inc {
  e.Num = <Add 1 e.Num>;
}


/**
  <Dec e.Num> == e.Num-1
*/
$ENTRY R5-Dec {
  e.Num = <Sub (e.Num) 1>;
}


/**
  <PrintLines (e.Line)*> == пусто
*/
$ENTRY R5-PrintLines {
  (e.Line) e.Lines =
    <Prout e.Line>
    <R5-PrintLines e.Lines>;

  /* пусто */ = /* ничего не печатаем */;
}


/**
  <SaveFile (e.FileName) (e.Line)*> == пусто
*/
$ENTRY R5-SaveFile {
  (e.FileName) e.Lines =
    <Open 'w' 20 e.FileName>
    <DoSaveFile e.Lines>
    <Close 20>;
}

DoSaveFile {
  (e.Line) e.Lines =
    <Put 20 e.Line>
    <DoSaveFile e.Lines>;

  /* пусто */ = /* всё */;
}


/**
  <LoadFile e.FileName> == (e.Line)*
*/
$ENTRY R5-LoadFile {
  e.FileName =
    <Open 'r' 20 e.FileName>
    <DoLoadFile <Get 20>>
    <Close 20>;
}

DoLoadFile {
  e.Line 0 = (e.Line);

  e.Line = (e.Line) <DoLoadFile <Get 20>>;
}


/**
  <OneOf t.Elem e.SampleElems> == True | False
*/
$ENTRY R5-OneOf {
  t.Elem e.Elems-B t.Elem e.Elems-E = True;
  t.Elem e.Elems = False;
}


/**
  <Builtins> == (e.FunctionName)*
*/
$ENTRY R5-Builtins {
  = ('+') ('-') ('*') ('/') ('%') <DoBuiltins <ListOfBuiltin>>;
}

DoBuiltins {
  (s.Number s.Name s.Type) e.Builtins
    = (<Explode s.Name>) <DoBuiltins e.Builtins>;

  /* пусто */ = /* пусто */;
}

/**
  <Unique t.Item*> == t.Item*
*/
$ENTRY R5-Unique {
  e.Items-B t.Item e.Items-M t.Item e.Items-E
    = e.Items-B <R5-Unique e.Items-M t.Item e.Items-E>;

  e.UniqueItems = e.UniqueItems;
}
