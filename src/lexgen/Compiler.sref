//FROM LibraryEx
$EXTERN Fetch, Seq, MapReduce, Map, Reduce, DelAccumulator;

//FROM Error
$EXTERN ErrorAt, WarningAt;


/*==============================================================================
   Разбор начинается с правила Root, множество :Any: задаёт множество всех
   символов, системная ошибка #TokenUnexpected, пользовательская ошибка
   #TokenError, неожиданный конец — #TokenUnexpectedEOF, в случае ошибки выб-
   расывается также #TokenAccum с содержимым аккумулятора на момент ошибки.
==============================================================================*/

//==============================================================================
// Генератор автомата
//==============================================================================

$ENTRY PrepareDFA {
  e.Elements =
    <Fetch
      e.Elements
      <Seq
        (Reduce
          {
            ((e.Sets) (e.Rules))
            (#Set t.SrcPos (e.Name) e.Content) =
              (
                (e.Sets (t.SrcPos (e.Name) #NotUsed e.Content))
                (e.Rules)
              );

            ((e.Sets) (e.Rules))
            (#Sentence t.SrcPos (e.Name) e.Content) =
              (
                (e.Sets)
                (e.Rules (t.SrcPos (e.Name) e.Content))
              );
          }
          ((/* sets */) (/* rules */))
        )
        {
          ((e.Sets) (e.Rules)) =
            (<ResolvingSets e.Sets>)
            (e.Rules);
        }
        SubstituteSetsToRules
        /* e.Rules */
        (Map NormalizeRule)
        PrepareFunctions
      >
    >;
}

UniqueSet {
  e.Set-B s.Repeated e.Set-M s.Repeated e.Set-E =
    e.Set-B <UniqueSet s.Repeated e.Set-M e.Set-E>;

  e.Set = e.Set;
}

ResolvingSets {
  e.Sets =
    <Fetch
      e.Sets
      <Seq
        (Map
          {
            (t.SrcPos (e.Name) s.Used #Any) =
              (t.SrcPos (e.Name) s.Used #Any);

            (t.SrcPos (e.Name) s.Used e.Content) =
              (t.SrcPos (e.Name) s.Used <NormalizeContent () e.Content>);
          }
        )
        SetCheckRecursion
        SetSubstitute
        {
          #Changed e.Sets^ = <ResolvingSets e.Sets>;

          #NotChanged e.Sets^ = e.Sets;
        }
      >
    >;
}

NormalizeContent {
  (e.Chars) (#Chars e.Symbols) e.Tail =
    <NormalizeContent
      (<UniqueSet e.Chars e.Symbols>) e.Tail
    >;

  (e.Chars) (#Set t.SrcPos e.Name) e.Tail =
    (#Set t.SrcPos e.Name) <NormalizeContent (e.Chars) e.Tail>;

  (e.Chars) #Any e.Tail = #Any;

  (e.Chars) = (#Chars e.Chars);
}

SetCheckRecursion {
  e.Sets-B
  (t.SetNamePos (e.Name) s.Used e.Content-B (#Set t.SrcPos e.Name) e.Content-E)
  e.Sets-E =
    // Рекурсивная зависимость множеств — не ошибка.
    <WarningAt t.SrcPos 'Recursive depended set ' e.Name>
    <SetCheckRecursion
      e.Sets-B (t.SetNamePos (e.Name) s.Used e.Content-B e.Content-E) e.Sets-E
    >;

  e.Sets = e.Sets;
}

SetSubstitute {
  e.Sets-B
  ( t.SetNamePos (e.Name) s.Used e.Content-B (#Set t.SrcPos 'Any') e.Content-E )
  e.Sets-E =
    #Changed e.Sets-B ( t.SetNamePos (e.Name) s.Used #Any ) e.Sets-E;

  e.Sets-B
  (
    t.SetNamePos (e.Name) s.UsedOuter
    e.Content-B (#Set t.SrcPos e.IncName) e.Content-E
  )
  e.Sets-M
  ( t.IncNamePos (e.IncName) s.UsedInner e.IncContent )
  e.Sets-E =
    #Changed
    e.Sets-B
    ( t.SetNamePos (e.Name) s.UsedOuter e.Content-B e.IncContent e.Content-E )
    e.Sets-M ( t.IncNamePos (e.IncName) #Used e.IncContent ) e.Sets-E;

  e.Sets-B
  ( t.IncNamePos (e.IncName) s.UsedInner e.IncContent )
  e.Sets-M
  (
    t.SetNamePos (e.Name) s.UsedOuter
    e.Content-B (#Set t.SrcPos e.IncName) e.Content-E
  )
  e.Sets-E =
    #Changed
    e.Sets-B ( t.IncNamePos (e.IncName) #Used e.IncContent ) e.Sets-M
    ( t.SetNamePos (e.Name) s.UsedOuter e.Content-B e.IncContent e.Content-E )
    e.Sets-E;

  e.Sets-B
  (
    t.SetNamePos (e.Name) s.Used
    e.Content-B (#Set t.SrcPos e.IncName) e.Content-E
  )
  e.Sets-E =
    <ErrorAt t.SrcPos 'set ' e.IncName ' not defined'>;

  e.Sets = #NotChanged e.Sets;
}

SubstituteSetsToRules {
  ( (t.SetNamePos (e.Name) s.Used (#Chars e.Content)) e.Sets )
  (
    e.Rules-B
    (t.SentNamePos (e.RuleName)
      e.Alternatives-B
      ( (#Set t.SrcPos e.Name) t.Flush t.NextState )
      e.Alternatives-E
    )
    e.Rules-E
  ) =
    <SubstituteSetsToRules
      ( (t.SetNamePos (e.Name) #Used (#Chars e.Content)) e.Sets )
      (
        e.Rules-B
        (t.SentNamePos (e.RuleName)
          e.Alternatives-B
          ( (#Chars e.Content) t.Flush t.NextState )
          e.Alternatives-E
        )
        e.Rules-E
      )
    >;

  ( (t.SetNamePos (e.Name) s.Used #Any) e.Sets )
  (
    e.Rules-B
    (t.SentNamePos (e.RuleName)
      e.Alternatives-B
      ( (#Set t.SrcPos e.Name) t.Flush t.NextState )
      e.Alternatives-E
    )
    e.Rules-E
  ) =
    <SubstituteSetsToRules
      ( (t.SetNamePos (e.Name) #Used #Any) e.Sets )
      (
        e.Rules-B
        (t.SentNamePos (e.RuleName)
          e.Alternatives-B
          ( #Any t.Flush t.NextState )
          e.Alternatives-E
        )
        e.Rules-E
      )
    >;

  // Для первого в списке множества не найдено вхождения
  ( (t.SetNamePos (e.Name) #NotUsed e.Content) e.Sets ) (e.Rules) =
    <WarningAt t.SetNamePos 'Set :' e.Name ': is not used'>
    <SubstituteSetsToRules (e.Sets) (e.Rules)>;

  ( (t.SetNamePos (e.Name) #Used e.Content) e.Sets ) (e.Rules) =
    <SubstituteSetsToRules (e.Sets) (e.Rules)>;

  ()
  (
    e.Rules-B
    (t.SentNamePos (e.RuleName)
      e.Alternatives-B
      ( (#Set t.SrcPos 'Any') t.Flush t.NextState )
      e.Alternatives-E
    )
    e.Rules-E
  ) =
    <SubstituteSetsToRules
      ()
      (
        e.Rules-B
        (t.SentNamePos (e.RuleName)
          e.Alternatives-B
          ( #Any t.Flush t.NextState )
          e.Alternatives-E
        )
        e.Rules-E
      )
    >;

  ()
  (
    e.Rules-B
    (t.SentNamePos (e.RuleName)
      e.Alternatives-B
      ( (#Set t.SrcPos e.Name) t.Flush t.NextState )
      e.Alternatives-E
    )
    e.Rules-E
  ) =
    <ErrorAt t.SrcPos 'Undeclared set ' e.Name>;

  () (e.Rules) = e.Rules;
}

SetDiff {
  (e.Set) (#Any) = ;

  (e.Set1-B s.Common e.Set1-E) (e.Set2-B s.Common e.Set2-E) =
    <SetDiff (e.Set1-B e.Set1-E) (e.Set2-B e.Set2-E)>;

  (e.SetDiff) (e.Set2) = e.SetDiff;
}

NormalizeAlternative {
  (e.Diff) ((#Chars e.Alternative) t.Flush t.Next) =
    (<UniqueSet e.Diff e.Alternative>)
    ((<SetDiff (e.Alternative) (e.Diff)>) t.Flush t.Next);

  (e.Diff) (#Any t.Flush t.Next) =
    (#Any)
    (#Any t.Flush t.Next);

  (e.Diff) ( #Empty t.Flush t.Next ) =
    (e.Diff) ( #Empty t.Flush t.Next );
}

SplitAlternatives {
  t.SentNamePos (e.Name) ( #Empty t.Flush t.Next ) =
    (#Empty t.Flush t.Next);

  t.SentNamePos (e.Name) ( #Any t.Flush t.Next ) =
    (#Any t.Flush t.Next);

  t.SentNamePos (e.Name) ( (/* пусто */) t.Flush t.Next ) =
    /* пусто */
    <WarningAt t.SentNamePos 'Rule ' e.Name ' has unreached alternatives'>;

  t.SentNamePos (e.Name) ( (s.Char) t.Flush t.Next ) =
    (s.Char t.Flush t.Next);

  t.SentNamePos (e.Name) ( (s.Char e.Tail) t.Flush t.Next ) =
    (s.Char t.Flush t.Next)
    <SplitAlternatives t.SentNamePos (e.Name) ( (e.Tail) t.Flush t.Next )>;
}

NormalizeRule {
  (t.SentNamePos (e.Name) e.Alternatives) =
    <Fetch
      e.Alternatives
      <Seq
        (MapReduce
          NormalizeAlternative
          ()
        )
        DelAccumulator
        (Map (SplitAlternatives t.SentNamePos (e.Name)))
        {
          e.Alternatives^ =
            (t.SentNamePos (e.Name) e.Alternatives);
        }
      >
    >;
}

PrepareFunctions {
  e.Rules =
    <DoPrepareFunctions
      (/* written */) (/* expected */ ((0 'runtime') 'Root'))
      e.Rules
    >;
}

ExtractExpected {
  ( t.Head t.Flush #Finitive ) = ;

  ( t.Head t.Flush ( t.SrcPos e.NextName ) ) =
    ( t.SrcPos e.NextName );
}

DoPrepareFunctions {
  (e.Written) () = ;

  (e.Written) () e.Rules =
    <Map
      {
        (t.SentNamePos (e.Name) e.Content) =
          <WarningAt t.SentNamePos 'Found unused rule ' e.Name>;
      }
      e.Rules
    >;

  (e.Written-B (e.Next) e.Written-E)
  ((t.SrcPos e.Next) e.Expected) e.Rules =
    <DoPrepareFunctions
      (e.Written-B (e.Next) e.Written-E)
      ( e.Expected ) e.Rules
    >;

  (e.Written) ((t.SrcPos e.NextRule) e.Expected)
  e.Rules-B (t.SentNamePos (e.NextRule) e.Alternatives) e.Rules-E =
    ((e.NextRule)
      <Map
        {
          (t.Head t.Flush #Finitive) =
            (t.Head t.Flush #Finitive);

          (t.Head t.Flush (t.GotoPos e.NextName)) =
            (t.Head t.Flush (e.NextName));
        }
        e.Alternatives
      >
    )
    <DoPrepareFunctions
      (e.Written (e.NextRule))
      ( e.Expected <Map ExtractExpected e.Alternatives> )
      e.Rules-B e.Rules-E
    >;

  (e.Written) ((t.SrcPos e.NextRule) e.Expected) e.Rules =
    <ErrorAt t.SrcPos 'Rule ' e.NextRule ' not found'>;
}
